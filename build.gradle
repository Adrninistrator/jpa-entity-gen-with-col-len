plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = "com.github.adrninistrator"
version = "0.0.1"
def projectName = 'jpa-entity-generator-enhance'
description = "${projectName}"

compileJava {
    sourceCompatibility = '1.8'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:26.0-jre'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'org.freemarker:freemarker:2.3.28'
    implementation 'org.yaml:snakeyaml:1.23'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    compileOnly gradleApi()
    compileOnly 'org.apache.maven:maven-plugin-api:3.5.4'

    testImplementation 'junit:junit:4.12'
    testImplementation 'javax.persistence:javax.persistence-api:2.2'
    testImplementation "org.apache.logging.log4j:log4j-api:2.11.1"
    testImplementation "org.apache.logging.log4j:log4j-core:2.11.1"
    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:2.11.1"
    testImplementation "org.slf4j:jcl-over-slf4j:1.7.28"
    testImplementation "mysql:mysql-connector-java:5.1.32"

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.2'
    testCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            exclude 'com/smartnews/jpa_entity_generator/maven/**'
        }

        resources {
            srcDir 'src/main/resources'
        }
    }

    test {
        java {
            srcDir 'src/test/java'
        }

        resources {
            srcDir 'src/test/resources'
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    // http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}


task sourcesJar(type: Jar) {
    doFirst {
        archiveClassifier.set('sources')
        from sourceSets.main.allJava
    }
}

task javadocJar(type: Jar) {
    doFirst {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "jpa-entity-generator-enhance"
                description = "Lombok-wired JPA entity source code generator, enchanced version."
                url = "https://github.com/Adrninistrator/jpa-entity-generator-enhance"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "Adrninistrator"
                        name = "zhengyi"
                        email = "zhengyiee@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/Adrninistrator/jpa-entity-generator-enhance.git"
                    developerConnection = "scm:git:https://github.com/Adrninistrator/jpa-entity-generator-enhance.git"
                    url = "https://github.com/Adrninistrator/jpa-entity-generator-enhance"
                }
            }
        }
    }
    repositories {
        maven {
            name 'sonatypeRepository'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
        maven {
            name = 'sonatypeSnapshotRepository'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

